#!/usr/bin/awk -f hildevs
#version $ABSD: hildevs,v 1.1.1.1 2008/08/26 14:41:53 root Exp $
#/*
# * Copyright (c) 2009 Michael Shalayeff
# * Copyright (c) 2003 Miodrag Vallat.
# * All rights reserved.
# *
# * Redistribution and use in source and binary forms, with or without
# * modification, are permitted provided that the following conditions
# * are met:
# * 1. Redistributions of source code must retain the above copyright
# *    notice, this list of conditions and the following disclaimer.
# * 2. Redistributions in binary form must reproduce the above copyright
# *    notice, this list of conditions and the following disclaimer in the
# *    documentation and/or other materials provided with the distribution.
# *
# * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
# * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# * DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
# * INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
# * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
# * ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# * POSSIBILITY OF SUCH DAMAGE.
# */

BEGIN {
	header = 0
}
/^#!/	{ next }
$1 == "#version" {
	VERSION = $0
	gsub("^#version[	 ]", "", VERSION)
	gsub("\\$", "", VERSION)

	printf("/*\n")
	printf(" * THIS FILE AUTOMATICALLY GENERATED.  DO NOT EDIT.\n")
	printf(" *\n")
	printf(" *\t%s\n", VERSION)
	printf(" */\n")

	next
}
$1 == "#keyboard" || $1 == "#mouse" || $1 == "#idmodule" || $1 == "#buttonbox" {

	sub(/^#/,"")
	if (header == 0) {
		printf("const struct hildevice hildevs[] = {\n")
		header = 1
	}

	printf("\t{ 0x%s, 0x%s, HIL_DEVICE_%s, \"",
	    $2, $3, toupper($1))

	# description, with optional ``#''-prefixed comments
	comment = 0
	i = 4
	f = i
	while (f <= NF) {
		if ($f == "#") {
			comment = 1
			printf ("\" },\t/*")
		} else {
			if (f > i)
				printf(" ")
			printf("%s", $f)
		}
		f++
	}
	if (comment)
		printf(" */\n");
	else
		printf("\" },\n")

	next
}
/^#/	{
	sub(/^#/,"")
	if ($0 == "")
		blanklines++
	if (blanklines < 2)
		print $0
}
END {
	printf("\t{ -1, -1, -1, NULL }\n")
	printf("};\n")
}

#buttonbox	00	1f	Keypad
#buttonbox	2f	2f	LPFK Button box
#buttonbox	30	33	Button box	# 31-33 rumored not to exist
#idmodule	34	34	ID module
#buttonbox	35	3f	Button box
#buttonbox	5c	5f	Barcode reader
#mouse		60	60	Single knob
#mouse		61	61	Nine knob	# can also be quadrature
#mouse		62	67	Quadrature
#mouse		68	6b	Mouse
#mouse		6c	6f	Trackball
#mouse		70	70	Knob box
#mouse		71	71	Spaceball
#mouse		88	8b	Touchpad
#mouse		8c	8f	Touchscreen
#mouse		90	97	Tablet
#mouse		98	98	MMII 1812 Tablet
#mouse		99	99	MMII 1201 Tablet
#keyboard	a0	bf	93-key keyboard
#keyboard	c0	df	109-key keyboard
#keyboard	e0	ff	87-key keyboard
