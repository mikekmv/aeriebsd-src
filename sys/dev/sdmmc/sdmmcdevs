#!/usr/bin/awk -f sdmmcdevs
#version $ABSD: sdmmcdevs,v 1.1.1.1 2008/08/26 14:42:14 root Exp $
#/*
# * Copyright (c) 2009 Michael Shalayeff
# * Copyright (c) 2006 Uwe Stuehler <uwe@openbsd.org>
# *
# * Permission to use, copy, modify, and distribute this software for any
# * purpose with or without fee is hereby granted, provided that the above
# * copyright notice and this permission notice appear in all copies.
# *
# * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
# *
# * Copyright (c) 1998, Christos Zoulas
# * Copyright (c) 1995, 1996 Christopher G. Demetriou
# * All rights reserved.
# *
# * Redistribution and use in source and binary forms, with or without
# * modification, are permitted provided that the following conditions
# * are met:
# * 1. Redistributions of source code must retain the above copyright
# *    notice, this list of conditions and the following disclaimer.
# * 2. Redistributions in binary form must reproduce the above copyright
# *    notice, this list of conditions and the following disclaimer in the
# *    documentation and/or other materials provided with the distribution.
# * 3. All advertising materials mentioning features or use of this software
# *    must display the following acknowledgement:
# *      This product includes software developed by Christopher G. Demetriou.
# *      This product includes software developed by Christos Zoulas
# * 4. The name of the author(s) may not be used to endorse or promote products
# *    derived from this software without specific prior written permission
# *
# * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
# * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
# * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
# * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
# * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
# * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
# */

function collectline(f, line) {
	oparen = 0
	line = ""
	while (f <= NF) {
		if ($f == "#") {
			line = line "("
			oparen = 1
			f++
			continue
		}
		if (oparen) {
			line = line $f
			if (f < NF)
				line = line " "
			f++
			continue
		}
		line = line $f
		if (f < NF)
			line = line " "
		f++
	}
	if (oparen)
		line = line ")"
	return line
}
BEGIN {
	nproducts = nvendors = 0
	hfile="sdmmcdevs.h"
}
/^#!/	{ next }
$1 == "#version" {
	VERSION = $0
	gsub("^#version[	 ]", "", VERSION)
	gsub("\\$", "", VERSION)

	printf("/*\n") > hfile
	printf(" * THIS FILE AUTOMATICALLY GENERATED.  DO NOT EDIT.\n") \
	    > hfile
	printf(" *\n") > hfile
	printf(" *\t%s\n", VERSION) > hfile
	printf(" */\n") > hfile

	next
}
$1 == "#vendor" {
	nvendors++

	vendorindex[$2] = nvendors;		# record index for this name, for later.
	vendors[nvendors, 1] = $2;		# name
	vendors[nvendors, 2] = $3;		# id
	printf("#define\tSDMMC_VENDOR_%s\t%s\t", vendors[nvendors, 1],
	    vendors[nvendors, 2]) > hfile
	vendors[nvendors, 3] = collectline(4, line)
	printf("/* %s */\n", vendors[nvendors, 3]) > hfile
	next
}
$1 == "#product" {
	nproducts++

	products[nproducts, 1] = $2;		# vendor name
	products[nproducts, 2] = $3;		# product id
	products[nproducts, 3] = $4;		# id

	f = 5;

	if ($4 == "{") {
		products[nproducts, 3] = "SDMMC_PRODUCT_INVALID"
		z = "{ "
		for (i = 0; i < 4; i++) {
			if (f <= NF) {
				gsub("&sp", " ", $f)
				gsub("&tab", "\t", $f)
				gsub("&nl", "\n", $f)
				z = z $f " "
				f++
			}
			else {
				if (i == 3)
					z = z "NULL "
				else
					z = z "NULL, "
			}
		}
		products[nproducts, 4] = z $f
		f++
	}
	else {
		products[nproducts, 4] = "{ NULL, NULL, NULL, NULL }"
	}
	printf("#define\tSDMMC_CIS_%s_%s\t%s\n",
	    products[nproducts, 1], products[nproducts, 2],
	    products[nproducts, 4]) > hfile
	printf("#define\tSDMMC_PRODUCT_%s_%s\t%s\n", products[nproducts, 1],
	    products[nproducts, 2], products[nproducts, 3]) > hfile

	next
}
/^#/	{
	sub(/^#/,"")
	print $0 > hfile
}

#/*
# * List of known SD card vendors
# */
#
#vendor CGUYS			0x0092	C-guys, Inc.
#vendor TOSHIBA			0x0098	Toshiba
#vendor SOCKETCOM		0x0104	Socket Communications, Inc.
#vendor ATHEROS			0x0271	Atheros
#vendor SYCHIP			0x02db	SyChip Inc.
#vendor SPECTEC			0x02fe	Spectec Computer Co., Ltd
#vendor GLOBALSAT		0x0501	Globalsat Technology Co.
#vendor MEDIATEK			0x037a	MediaTek Inc.
#vendor ABOCOM			0x13d1	AboCom Systems, Inc.
#
#/*
# * List of known products, grouped by vendor
# */
#
#/* AboCom Systems, Inc. */
#product ABOCOM SDW11G		0xac02	SDW11G
#
#/* Atheros */
#product ATHEROS	AR6001_8	0x0108	AR6001
#product ATHEROS	AR6001_9	0x0109	AR6001
#product ATHEROS	AR6001_a	0x010a	AR6001
#product ATHEROS	AR6001_b	0x010b	AR6001
#
#/* C-guys, Inc. */
#product CGUYS TIACX100		0x0001	TI ACX100 SD-Link11b WiFi Card
#product CGUYS SDFMRADIO2	0x0005	C-guys SD FM Radio 2
#product CGUYS SDFMRADIO		0x5544	C-guys SD FM Radio
#
#/* Globalsat Technology Co. */
#product GLOBALSAT SD501		0xf501	Globalsat SD-501 GPS Card
#
#/* MediaTek Inc. */
#product MEDIATEK S2YWLAN	0x5911	MediaTek/Spectec WLAN-11b/g
#
#/* Spectec Computer Co., Ltd */
#product SPECTEC SDW820		0x2128	Spectec SDIO WLAN Card
#
#/* SyChip Inc. */
#product SYCHIP WLAN6060SD	0x0002	SyChip Pegasus WLAN SDIO Card
#
#/* Toshiba */
#product TOSHIBA SDBTCARD1	0x0001	Toshiba SD BT Card 1
#product TOSHIBA SDBTCARD2	0x0002	Toshiba SD BT Card 2
#product TOSHIBA SDBTCARD3	0x0003	Toshiba SD BT Card 3
#
#/* Socket Communications, Inc. */
#product SOCKETCOM SDSCANNER	0x005e	Socket SD Scanner
#product SOCKETCOM BTCARD	0x00c5	Socket BT Card
